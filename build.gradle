plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.21'
    id 'org.jetbrains.grammarkit' version '2020.2.1'
}

group 'com.github.vitalibo'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'org.testng', name: 'testng', version: '7.1.0'

    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    testCompileOnly 'org.projectlombok:lombok:1.18.12'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
}

sourceSets.main.java.srcDirs 'src/main/gen'

intellij {
    version '2020.1.3'
    pluginName 'BitBake'
    patchPluginXml {
        sinceBuild '201.0'
        untilBuild '210.*'
    }
}

patchPluginXml {
    changeNotes """
      Add change notes here.<br>
      <em>most HTML tags may be used</em>"""
}

import org.jetbrains.grammarkit.tasks.GenerateLexer
import org.jetbrains.grammarkit.tasks.GenerateParser

task generateBitBakeLexer(type: GenerateLexer) {
    source = "src/main/java/com/github/vitalibo/intellij/bitbake/BitBake.flex"
    targetDir = 'src/main/gen/com/github/vitalibo/intellij/bitbake'
    targetClass = "BitBakeLexer"
    purgeOldFiles = true
}

task generateBitBakeParser(type: GenerateParser) {
    source = "src/main/java/com/github/vitalibo/intellij/bitbake/BitBake.bnf"
    targetRoot = 'src/main/gen'
    pathToParser = '/com/github/vitalibo/intellij/bitbake/parser/BitBakeParser.java'
    pathToPsiRoot = '/com/github/vitalibo/intellij/bitbake/psi'
    purgeOldFiles = true
}

compileJava {
    dependsOn generateBitBakeLexer
    dependsOn generateBitBakeParser
}
